plugins {
    id 'maven-publish'
    id 'distribution'
}

group = 'de.nrw.schule.svws'
version = '1.0.0'


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/SVWS-NRW/Schild-NRW-3-Reports"
            credentials {
            	username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenZip(MavenPublication) {
            artifact  'build/Schild-NRW-Reports.zip'
            artifactId = 'svws-schild-nrw-reports'
            pom {
                name = 'SVWS Schild-NRW-Reports'
                description = 'Diese Bibliothek stellt das ZIP-Archiv für den Schild3.0-Installer für die Dateien der Schild-NRW-Reports zur Verfügung.'
                url = 'http://www.svws.nrw.de'
                licenses {
                    license {
                        name = 'BSD 3 License'
                        url = 'https://opensource.org/license/bsd-3-clause'
                    }
                }
                scm {
                    connection = 'scm:git@github.com/SVWS-NRW/Schild-NRW-3-Reports'
                    developerConnection = 'scm:git@github.com/SVWS-NRW/Schild-NRW-3-Reports.git'
                    url = 'https://github.com/SVWS-NRW/Schild-NRW-3-Reports.git'
                }
            }
        }
    }
}

task copyBasisreports(type: Copy) {
    into "$buildDir/output/Basisreports"
    from "Basisreports"
}

task copySerienbriefe(type: Copy) {
    into "$buildDir/output/Serienbriefe"
    from "Serienbriefe"
}

task doZip(type: Zip) {
	metadataCharset 'utf-8'
	archiveFileName = "Schild-NRW-Reports.zip"
    destinationDirectory = file("$buildDir")
    from "$buildDir/output"
}

doZip.dependsOn copyBasisreports
doZip.dependsOn copySerienbriefe
build.dependsOn doZip
generatePomFileForMavenZipPublication.dependsOn build
